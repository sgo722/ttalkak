buildscript {
    ext {
        restdocsApiSpecVersion = '0.18.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'kr.kro.ttalkak'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}


ext {
    set('springCloudVersion', "2023.0.3")
    set('snippetsDir', file("build/generated-snippets"))
}

openapi3 {
    servers = [
            { url = "https://ttalkak.com" }
    ]
    title = 'TTALKAK API Documentation'
    description = '딸깍 프로젝트 배포 API 문서입니다.'
    version = '0.0.1'
    outputFileNamePrefix = 'ttalkak-deployment-api-docs'
    format = 'yaml'
    outputDirectory = 'build/resources/test/docs'
}


dependencies {
    // 스프링 웹 의존성
    implementation 'org.springframework.boot:spring-boot-starter-web'

    // 데이터베이스 관련 의존성
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    runtimeOnly 'org.postgresql:postgresql'
    runtimeOnly 'com.h2database:h2'

    // 스프링 클라우드 의존성
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation 'org.springframework.kafka:spring-kafka'


    // 개발 도구 의존성
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'

    // 테스트 의존성
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "com.epages:restdocs-api-spec-mockmvc:${restdocsApiSpecVersion}"
    testImplementation 'com.google.code.gson:gson:2.11.0'
    testImplementation 'org.projectlombok:lombok'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'


    // 로그 id 수집
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation('io.micrometer:micrometer-tracing-bridge-brave') {
        // 프로젝트에서 Zipkin을 사용하지 않는 경우
        exclude group: 'io.zipkin.reporter2'
    }

    // logstash와 통신하여 로그를 보내기 위한 설정
    // https://mvnrepository.com/artifact/net.logstash.logback/logstash-logback-encoder
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    testImplementation 'ch.qos.logback:logback-classic:1.5.7'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
    implementation 'ch.qos.logback:logback-core:1.5.7'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-access
    implementation 'ch.qos.logback:logback-access:1.5.7'

    // 예외 의존성
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // swagger 의존성
//    implementation 'io.springfox:springfox-boot-starter:3.0.0'
//
//    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0' // 최신 버전으로 교체 가능

}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    outputs.dir snippetsDir
}

tasks.register('openapi3-security-schemes') {
    dependsOn 'openapi3'
    doFirst {
        def swaggerUIFile = file("${openapi3.outputDirectory}/${openapi3.outputFileNamePrefix}.${openapi3.format}")

        def securitySchemesContent = "  securitySchemes:\n" +         \
                                             "    APIKey:\n" +         \
                                             "      type: apiKey\n" +         \
                                             "      name: Authorization\n" +         \
                                             "      in: header\n" +        \
                                             "security:\n" +
                "  - APIKey: []  # Apply the security scheme here"

        swaggerUIFile.append securitySchemesContent
    }
}


tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test
}


tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}