buildscript {
    ext {
        restdocsApiSpecVersion = '0.18.2'
    }
}

plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id "org.sonarqube" version "4.0.0.2929"
    id 'org.jetbrains.kotlin.jvm'
    id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
}

group = 'com.ttalkak'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2023.0.3")
}

openapi3 {
    servers = [
            { url = "https://ttalkak.com" }
    ]
    title = 'TTALKAK API Documentation'
    description = '딸깍 프로젝트 배포 API 문서입니다.'
    version = '0.0.1'
    outputFileNamePrefix = 'ttalkak-user-api-docs'
    format = 'yaml'
    outputDirectory = 'build/resources/test/docs'
}

dependencies {
    // 스프링 웹 의존성
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // 인증 관련 의존성
    implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.12.6'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // 데이터베이스 관련 의존성
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.kafka:spring-kafka'
    testImplementation 'org.springframework.kafka:spring-kafka-test'
    runtimeOnly 'org.postgresql:postgresql'

    // 스프링 클라우드 의존성
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-config'

    // 개발 도구 의존성
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // 테스트 의존성
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 로그 id 수집
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation('io.micrometer:micrometer-tracing-bridge-brave') {
        // 프로젝트에서 Zipkin을 사용하지 않는 경우
        exclude group: 'io.zipkin.reporter2'
    }

    // logstash와 통신하여 로그를 보내기 위한 설정
    // https://mvnrepository.com/artifact/net.logstash.logback/logstash-logback-encoder
    implementation 'net.logstash.logback:logstash-logback-encoder:7.4'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    testImplementation 'ch.qos.logback:logback-classic:1.5.7'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-core
    implementation 'ch.qos.logback:logback-core:1.5.7'
    // https://mvnrepository.com/artifact/ch.qos.logback/logback-access
    implementation 'ch.qos.logback:logback-access:1.5.7'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation "com.epages:restdocs-api-spec-mockmvc:${restdocsApiSpecVersion}"
}

tasks.register('openapi3-security-schemes') {
    dependsOn 'openapi3'
    doFirst {
        def swaggerUIFile = file("${openapi3.outputDirectory}/${openapi3.outputFileNamePrefix}.${openapi3.format}")

        def securitySchemesContent = "  securitySchemes:\n" +         \
                                             "    APIKey:\n" +         \
                                             "      type: apiKey\n" +         \
                                             "      name: Authorization\n" +         \
                                             "      in: header\n" +        \
                                             "security:\n" +
                "  - APIKey: []"

        swaggerUIFile.append securitySchemesContent
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}
